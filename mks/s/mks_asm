
;	GET C:h.SWINames
; Gerph: I don't have C:h.SWInames, which appears to be an assembler header for the SWI names.
;        The ones we use are below.

XOS_EnterOS         *   0x20016
XOS_SpriteOp        *   0x2002E
XDrawFile_Render    *   0x65540


	GET s.asmhdr

	AREA	|C$$code|, CODE, READONLY

	FUNC render_aff		; R0->sprite
	                        ; R1->drawfile
	                        ; R2= length of drawfile
	                        ; R3->transformation matrix
	STMFD R13!,{R14}
	MOV R12,PC
	SWI XOS_EnterOS
	STMFD R13!,{R14}
	 ADR R14,args
	 STMIA R14,{R1-R3}
	 MOV R1,R0
	 ADD R2,R1,#16
	 MOV R3,#0
	 MOV R0,#&23C
	 SWI XOS_SpriteOp	; has to be in SVC mode, otherwise taskwindow crashes
	 BVS exit
	 STMFD R13!,{R0-R4}
	  MOV R0,#0
	  LDR R1,draw
	  LDR R2,len
	  LDR R3,trans
	  MOV R4,#0
	  SWI XDrawFile_Render
	 LDMFD R13!,{R0-R4}
	 SWI XOS_SpriteOp
exit	LDMFD R13!,{R14}
	TEQP R12,#0		; back to USR
	NOP
        LDMFD R13!,{PC}^
	FUNCEND

args
draw    DCD 0
len     DCD 0
trans   DCD 0

        FUNC magnitude		; R0=X
        			; On exit:
        			; R0=smallest N, where 2^N >= X (unsigned)
        MOV R1,R0
        MOV R2,#1
        MOV R0,#0
1	 CMP R2,R1
         ADDCC R0,R0,#1
         MOVCCS R2,R2,LSL#1	; worst case 31 loops
        BCC %b1
        MOVS PC,R14
        FUNCEND

        FUNC magnitude2		; R0=X
        			; On exit:
        			; R0=smallest N, where 2^N >= X (unsigned)
        SUB R0,R0,#1
        MOV R1,#16
        MOV R2,#16
        MVN R3,#0
        MOV R3,R3,LSR#16
1        TST R0,R3,LSL R1
         SUBEQ R1,R1,R2
         ADD R1,R1,R2,LSR#1	; worst case 5 loops
         MOVS R2,R2,LSR#1
         MOV R3,R3,LSR R2
        BNE %b1
        ADD R0,R1,#1
        MOVS PC,R14
        FUNCEND

        FUNC magnitude3		; R0=X
        			; On exit:
        			; R0=smallest N, where 2^N >= X (unsigned)
        LDR R2,magic
        ORR R1,R0,R0,LSR#16
        ORR R1,R1,R1,LSR#8
        ORR R1,R1,R1,LSR#4
        ORR R1,R1,R1,LSR#2
        ORR R1,R1,R1,LSR#1
        EOR R1,R1,R1,LSR#1
        MUL R1,R2,R1
        AND R0,R1,#31
        MOVS PC,R14
        FUNCEND

magic   DCD 0x0450FEBF

	EXPORT tab8_32
tab8_32
	GET s.tab8_32g
        ALIGN 4

	END
